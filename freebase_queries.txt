# All entities in Freebase with name, description, links, types, notable types, aliases, and keys
# https://qlever.cs.uni-freiburg.de/freebase/mssAnA

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fb: <http://rdf.freebase.com/ns/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT
  ?ent 
  ?ent_name
  ?ent_description 
  ?links
  (GROUP_CONCAT(DISTINCT ?type; SEPARATOR="; ") AS ?types)
  (GROUP_CONCAT(DISTINCT ?notable; SEPARATOR="; ") AS ?notables)
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR="; ") AS ?aliases)
  (GROUP_CONCAT(DISTINCT ?key; SEPARATOR="; ") AS ?keys)
WHERE 
{
  ?ent fb:type.object.name ?ent_name .
  FILTER(LANG(?ent_name) = "en")
  OPTIONAL {
    ?ent fb:common.topic.description ?ent_description .
	FILTER(LANG(?ent_description) = "en") .
  }
  OPTIONAL {
    ?ent fb:freebase.type_profile.instance_count ?links .
  }
  OPTIONAL {
    ?ent fb:type.object.type ?type_ .
	?type_ fb:type.object.name ?type .
	FILTER(LANG(?type) = "en")
  }
  OPTIONAL {
    ?ent fb:common.topic.notable_types ?notable_ .
	?notable_ fb:type.object.name ?notable .
	FILTER(LANG(?notable) = "en")
  }
  OPTIONAL {
    ?ent fb:type.object.key ?key .
	FILTER(LANG(?key) = "en")
  }
  OPTIONAL {
    ?ent fb:common.topic.alias ?alias .
	FILTER(LANG(?alias) = "en")
  }
}
GROUP BY ?ent ?ent_name ?ent_description ?links
ORDER BY DESC(?links)

# All properties in Freebase with name, count and domain
# https://qlever.cs.uni-freiburg.de/freebase/wKemSV

PREFIX fb: <http://rdf.freebase.com/ns/>
SELECT DISTINCT
  ?p 
  ?p_label
  ?p_count
  ?domain
WHERE 
{
  { SELECT ?p (COUNT(?p) as ?p_count) WHERE { ?s ?p ?o } GROUP BY ?p }
  ?p fb:type.object.name ?p_label .
  FILTER(LANG(?p_label) = "en") .
  ?p fb:type.object.type fb:type.property .
  OPTIONAL {
    ?p fb:type.property.schema ?domain_ .
    ?domain_ fb:type.object.name ?domain .
    FILTER(LANG(?domain) = "en")
  }
}
GROUP BY ?p ?p_label ?p_count ?domain
ORDER BY DESC(?p_count)
