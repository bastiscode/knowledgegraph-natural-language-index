# All entities in Wikidata with name, description, number of links, and aliases
# entity is defined as something that has a pic, or is a subclass or instance of something
# that has a pic, or something that has a Wikipedia article

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT 
  ?ent 
  ?ent_name 
  ?ent_description
  (MAX(?sitelinks) AS ?links)
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR = "; ") AS ?aliases)
  WHERE 
{
  {
    SELECT ?ent WHERE {
      ?ent wdt:P279*/wdt:P18 ?pic .
	} GROUP BY ?ent
  } UNION {
    SELECT ?ent WHERE {
      ?ent wdt:P31*/wdt:P18 ?pic .
	} GROUP BY ?ent
  } UNION { 
    SELECT ?ent WHERE {
      ?ent wdt:P279*/^schema:about/schema:isPartOf ?wiki .
      FILTER(REGEX(STR(?wiki), "^https?://.*.wikipedia.org")) .
	} GROUP BY ?ent
  }
  ?ent rdfs:label ?ent_name .
  FILTER (LANG(?ent_name) = "en") .
  OPTIONAL {
    ?ent ^schema:about/wikibase:sitelinks ?sitelinks . 
  }
  OPTIONAL {
    ?ent schema:description ?ent_description .
    FILTER (LANG(?ent_description) = "en") .
  }
  OPTIONAL {
    ?ent skos:altLabel ?alias .
    FILTER (LANG(?alias) = "en") .
  }
}
GROUP BY ?ent ?ent_name ?ent_description
ORDER BY DESC(?links)

# All entity redirects

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT 
  ?ent 
  (GROUP_CONCAT(DISTINCT ?redir; SEPARATOR = "; ") AS ?redirs)
WHERE {
  ?redir owl:sameAs ?ent .
  FILTER(REGEX(STR(?ent), "entity/Q\\d+"))
}
GROUP BY ?ent

# All direct properties in Wikidata with name, aliases, and inverse properties

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT 
  ?p 
  ?prop_label 
  (GROUP_CONCAT(DISTINCT ?prop_alias; SEPARATOR = "; ") AS ?prop_aliases) 
  (GROUP_CONCAT(DISTINCT ?inv_prop; SEPARATOR = "; ") AS ?inv_props)
  WHERE
{
  ?prop wikibase:directClaim ?p .
  ?prop rdfs:label ?prop_label . 
  FILTER(LANG(?prop_label) = "en") .
  OPTIONAL {
    ?prop skos:altLabel ?prop_alias .
    FILTER(LANG(?prop_alias) = "en") .
  }
  OPTIONAL {
    ?prop wdt:P1696 ?inv_prop .
  }
}
GROUP BY ?p ?prop_label
