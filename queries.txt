# All entities in Wikidata with name, description, number of links, and aliases

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT 
  ?entity 
  ?entity_name 
  ?entity_description
  (MAX(?sitelinks) AS ?links)
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR = "; ") AS ?aliases)
  (GROUP_CONCAT(DISTINCT ?redir; SEPARATOR = "; ") AS ?redirs)
  WHERE 
{
  { 
    ?entity wdt:P18 ?pic . 
  }
  UNION
  { 
    ?article schema:about ?entity .
    ?article schema:isPartOf ?wiki .
    FILTER(REGEX(STR(?wiki), "^https?://.*.wikipedia.org")) .
  }
  ?entity ^schema:about/wikibase:sitelinks ?sitelinks . 
  ?entity rdfs:label ?entity_name .
  FILTER (LANG(?entity_name) = "en") .
  OPTIONAL {
    ?entity schema:description ?entity_description .
    FILTER (LANG(?entity_description) = "en") .
  }
  OPTIONAL {
    ?entity skos:altLabel ?alias .
    FILTER (LANG(?alias) = "en") .
  }
  OPTIONAL {
    ?redir owl:sameAs ?entity .
  }
}
GROUP BY ?entity ?entity_name ?entity_description
ORDER BY DESC(?links)

# All entity redirects

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT 
  ?ent 
  (GROUP_CONCAT(DISTINCT ?redir; SEPARATOR = "; ") AS ?redirs)
WHERE {
  ?redir owl:sameAs ?ent .
  FILTER(REGEX(STR(?ent), "entity/Q\d+"))
}
GROUP BY ?ent

# All direct properties in Wikidata with name, aliases, and inverse properties

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
SELECT 
  ?p 
  ?prop_label 
  (GROUP_CONCAT(DISTINCT ?prop_alias; SEPARATOR = "; ") AS ?prop_aliases) 
  (GROUP_CONCAT(DISTINCT ?inv_prop; SEPARATOR = "; ") AS ?inv_props)
  WHERE
{
  ?prop wikibase:directClaim ?p .
  ?prop rdfs:label ?prop_label . 
  FILTER(LANG(?prop_label) = "en") .
  OPTIONAL {
    ?prop skos:altLabel ?prop_alias .
    FILTER(LANG(?prop_alias) = "en") .
  }
  OPTIONAL {
    ?prop wdt:P1696 ?inv_prop .
  }
}
GROUP BY ?p ?prop_label
