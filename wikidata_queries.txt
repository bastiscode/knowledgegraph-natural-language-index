# All entities in Wikidata with name, description, number of links, and aliases
# entity is defined as something that has a pic, or is a subclass or instance of something
# that has a pic, or something that has a Wikipedia article

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
SELECT 
  ?ent 
  ?ent_name 
  ?ent_description
  ?links
  (GROUP_CONCAT(DISTINCT ?type; SEPARATOR = "; ") AS ?types)
  (GROUP_CONCAT(DISTINCT ?alias; SEPARATOR = "; ") AS ?aliases)
  WHERE 
{
  {
    SELECT ?ent WHERE {
      ?ent wdt:P279*/wdt:P18 ?pic .
	} GROUP BY ?ent
  } UNION {
    SELECT ?ent WHERE {
      ?ent wdt:P31*/wdt:P18 ?pic .
	} GROUP BY ?ent
  } UNION { 
    SELECT ?ent WHERE {
      ?ent ^schema:about/schema:isPartOf ?wiki .
      FILTER(REGEX(STR(?wiki), "^https?://.*.wikipedia.org")) .
	} GROUP BY ?ent
  }
  ?ent rdfs:label ?ent_name .
  FILTER(LANG(?ent_name) = "en") .
  FILTER(REGEX(STR(?ent), "entity/Q\\d+")) .
  OPTIONAL {
    ?ent ^schema:about/wikibase:sitelinks ?links . 
  }
  OPTIONAL {
    {
      ?ent wdt:P31 ?type .
	} UNION {
	  ?ent wdt:P279 ?type .
	}
  }
  OPTIONAL {
    ?ent schema:description ?ent_description .
    FILTER (LANG(?ent_description) = "en") .
  }
  OPTIONAL {
    ?ent skos:altLabel ?alias .
    FILTER (LANG(?alias) = "en") .
  }
}
GROUP BY ?ent ?ent_name ?ent_description ?links
ORDER BY DESC(?links)

# All entity redirects

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT 
  ?ent 
  (GROUP_CONCAT(DISTINCT ?redir; SEPARATOR = "\t") AS ?redirs)
WHERE {
  ?redir owl:sameAs ?ent .
  FILTER(REGEX(STR(?ent), "entity/Q\\d+"))
}
GROUP BY ?ent

# All direct properties in Wikidata with name, count, aliases, and inverse properties

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT 
  ?p 
  ?p_label
  ?p_count
  (GROUP_CONCAT(DISTINCT ?p_alias; SEPARATOR = "; ") AS ?p_aliases) 
  (GROUP_CONCAT(DISTINCT ?p_inv; SEPARATOR = "; ") AS ?p_invs)
  WHERE
{
  ?p wikibase:directClaim ?claim .
  ?p rdfs:label ?p_label .
  FILTER(LANG(?p_label) = "en") .
  BIND(0 AS ?p_count) .
  OPTIONAL {
    ?p skos:altLabel ?p_alias .
    FILTER(LANG(?p_alias) = "en") .
  }
  OPTIONAL {
    ?p wdt:P1696 ?p_inv .
  }
}
GROUP BY ?p ?p_label ?p_count
